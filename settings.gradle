include 'desktop', 'core', 'net', 'server', 'ios', 'annotations', 'tools', 'tests'

def use = { String name, String path ->
    include(name)
    project(name).projectDir = new File(settingsDir, path)
}

Properties properties = new Properties()

if(new File(settingsDir, 'local.properties').exists()){
    properties.load(new File(settingsDir, 'local.properties').newDataInputStream())

    if(properties.containsKey("sdk.dir")){
        include 'android'
    }else{
        println("No android SDK dir found Not loading Android module.")
    }
}else{
    println("No local.properties found. Not loading Android module.")
}

if(System.properties["release"] == null || System.properties["release"] == "false"){
    if (new File(settingsDir, '../Arc').exists()) {
        use(':Arc', '../Arc')
        use(':Arc:arc-core', '../Arc/arc-core')
        use(':Arc:extensions', '../Arc/extensions')
        use(':Arc:extensions:freetype', '../Arc/extensions/freetype')
        use(':Arc:backends', '../Arc/backends')
        use(':Arc:backends:backend-lwjgl3', '../Arc/backends/backend-lwjgl3')
    }
    if (new File(settingsDir, '../debug').exists()) {
        use(':debug', '../debug')
    }
}else{
    println("Not including local repositories.")
}
