include 'desktop', 'core', 'kryonet', 'server', 'ios', 'annotations', 'tools', 'tests'

Properties properties = new Properties()

if(new File(settingsDir, 'local.properties').exists()){
    properties.load(new File(settingsDir, 'local.properties').newDataInputStream())

    if(properties.containsKey("sdk.dir")){
        include 'android'
    }else{
        println("No android SDK dir found Not loading Android module.")
    }
}else{
    println("No local.properties found. Not loading Android module.")
}

if(System.properties["release"] == null || System.properties["release"] == "false"){
    if (new File(settingsDir, '../Arc').exists()) {
        //ARC ARC ARC ARC ARC
        include ':Arc'
        project(':Arc').projectDir = new File(settingsDir, '../Arc')
        include ':Arc:core'
        project(':Arc:core').projectDir = new File(settingsDir, '../Arc/core')
        include ':Arc:extensions'
        project(':Arc:extensions').projectDir = new File(settingsDir, '../Arc/extensions')
        include ':Arc:extensions:freetype'
        project(':Arc:extensions:freetype').projectDir = new File(settingsDir, '../Arc/extensions/freetype')
    }
    if (new File(settingsDir, '../GDXGifRecorder').exists()) {
        include ':GDXGifRecorder'
        project(':GDXGifRecorder').projectDir = new File(settingsDir, '../GDXGifRecorder')
    }
    if (new File(settingsDir, '../debug').exists()) {
        include ':debug'
        project(':debug').projectDir = new File(settingsDir, '../debug')
    }
}else{
    println("Not including local repositories.")
}
